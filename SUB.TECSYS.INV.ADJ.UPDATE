SUBROUTINE SUB.TECSYS.INV.ADJ.UPDATE(FILE.DATA,DATA.TYPE,START.MARKER,END.MARKER)
*********************************************************************
*
* PARSE INCOMING DATA FROM TECSYS                                   *
*                                                                   
*********************************************************************
CALL IQ.FILES
COMMON FILES(200)

AM = CHAR(254)
VM = CHAR(253)
SVM = CHAR(252)

* PICK FILE OPENS
OPEN "INVENTORY" TO F.INVENTORY ELSE DEBUG
OPEN "PRODUCT-MASTER" TO F.PRODUCT.MASTER ELSE DEBUG
OPEN "PRODUCT-CATEGORY" TO F.PRODUCT.CATEGORY ELSE DEBUG
OPEN "PROD-HISTORY" TO F.PROD.HISTORY ELSE DEBUG
OPEN "PROD-HISTORY-PART" TO F.PROD.HISTORY.PART ELSE DEBUG
OPEN "COMPANY" TO F.COMPANY ELSE DEBUG
OPEN "AR-GL" TO F.AR.GL ELSE DEBUG

* GLOBAL VARIABLES
REC.DATE = DATE()
REC.TIME = TIME()
CUR.MONTH = OCONV(REC.DATE,"D2/")      ;*  -  Used for update to PH<18>
MMYY = CUR.MONTH[1,2]:CUR.MONTH[7,2]      ;*   --  Used for key when writing to PHP
MONTH.NO = CUR.MONTH[1,2]


SPECIAL.PART.LIST=''
SPECIAL.PART.LIST<1> = "BUSINESSPR"
SPECIAL.PART.LIST<2> = "DONATIONPR"
SPECIAL.PART.LIST<3> = "BILLING ADJ"
SPECIAL.PART.LIST<4> = "BUSINESSMI"


************************************
* START OF PROCESSING OF THE ORDER *
************************************

* ORDER DATA ARRAY VARIABLES *
INV.REC=''     ;   * USED TO READ IN THE ORIGINAL ITEM DATA FROM THE INVENTORY FILE
DATA.UPDATE='' ; * THIS IS UPDATED WITH THE WMS INFORMATION AS IT IS PROCESSED
ITEM.ID=''
PHP.REC=''
BRANCH='1'


**************************************
* INITIALIZE AND LOAD DATA FROM PICK *
**************************************
LOCATE "Item" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
    IF POS > START.MARKER AND POS < END.MARKER THEN
        ITEM.ID=FILE.DATA<3,POS>
    END ELSE
        
        FIND.TAG = "OutboundOrder":AM:"InboundOrder"
        TAG.DATA = ''
        CALL SUB.TECSYS.LOC.TAG.DATA(FILE.DATA,FIND.TAG,TAG.DATA,START.MARKER,END.MARKER,KEY)

        IF TAG.DATA<1> # '' AND TAG.DATA<1> # '0' THEN
            TAG.DATA = FIND.TAG<1>:'-':TAG.DATA<1>
        END ELSE
            IF TAG.DATA<2> # '' AND TAG.DATA<2> # '0' THEN
                TAG.DATA = FIND.TAG<2>:'-':TAG.DATA<2>
            END ELSE
                TAG.DATA = "NO TIED ORDER"
            END
        END

        EMAIL.LIST = 'inv-control@goblueteam.com'
        EMAIL.LIST<-1> = 'support@goblueteam.com'
        CUSTOM.FROM = ''
        CUSTOM.SUBJECT='NO ITEM FOUND WITHIN BOUNDS ON ORDER# ': TAG.DATA
        CUSTOM.BODY = ''
        
        CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)
        RELEASE
        RETURN
    END
END ELSE
    * IF WE CANT' FIND AN ITEM TAG THEN WE CAN'T LOAD PART DATA!! WHAT SHOULD WE DO HERE, SEND AN EMAIL?

        FIND.TAG = "OutboundOrder":AM:"InboundOrder"
        TAG.DATA = ''
        CALL SUB.TECSYS.LOC.TAG.DATA(FILE.DATA,FIND.TAG,TAG.DATA,START.MARKER,END.MARKER,KEY)

        IF TAG.DATA<1> # '' AND TAG.DATA<1> # '0' THEN
            TAG.DATA = FIND.TAG<1>:'-':TAG.DATA<1>
        END ELSE
            IF TAG.DATA<2> # '' AND TAG.DATA<2> # '0' THEN
                TAG.DATA = FIND.TAG<2>:'-':TAG.DATA<2>
            END ELSE
                TAG.DATA = "NO TIED ORDER"
            END
        END

        EMAIL.LIST = 'inv-control@goblueteam.com'
        EMAIL.LIST<-1> = 'support@goblueteam.com'
        CUSTOM.FROM = ''
        CUSTOM.SUBJECT='NO <Item> TAG FOUND IN ORDER ': TAG.DATA
        CUSTOM.BODY = ''
        
        CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)
    RELEASE
    RETURN
END

LOCATE ITEM.ID IN SPECIAL.PART.LIST SETTING JUNK THEN 
        EMAIL.LIST = 'JASH@WNS1.COM'
        CUSTOM.FROM = ''
        CUSTOM.SUBJECT = 'SPECIAL PART ADJ FOUND'
        CUSTOM.BODY= 'SPECIAL PART FOUND: ':ITEM.ID
        CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)
    RELEASE
    RETURN
END ELSE
    READU INV.REC FROM F.INVENTORY,ITEM.ID:"*":BRANCH THEN    ;  * LOCK AND READ IN ORIGINAL ITEM DATA
        COST = INV.REC<7>
    END ELSE
        EMAIL.LIST = 'inv-control@goblueteam.com'
        EMAIL.LIST<-1> = 'support@goblueteam.com'
        CUSTOM.FROM = ''
        CUSTOM.SUBJECT='UNABLE TO READ INVENTORY RECORD FILE FOR PART# ': ITEM.ID
        CUSTOM.BODY = ''
        
        CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)
        RELEASE
        RETURN
    END
END


*******************************************************
* THIS IS WHERE WE LOOK UP AND ASSIGN THE DATA FIELDS *
*******************************************************
* THESE VARIABLES ARE THE PIECES OF INFORMATION WE ARE GOING TO LOCATE IN THE UPDATE *
TASK=DATA.TYPE<1,1>
REASON=DATA.TYPE<1,2>
PKG.CODE=''
TAG=''
HOLD.CODE=''
DAMAGED=''
ORIG.QTY=''
TRANS.QTY=''
NEW.QTY=''
USER.ID=''
LOT=''
OUT.ORD.NUM=''
OUT.ORD.TYPE=''
OUT.ORD.LINE=''
ORD.STAT=''
IN.ORD.NUM=''
IN.ORD.TYPE=''
IN.ORD.LINE=''
ORD.LINE.COMP=''
CONTAINER=''
LOCATION=''
RECV.LOC=''
SER.NUM=''
ITEM.EXP=''
NEG.QOH=''

    LOCATE "Warehouse" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            BRANCH=FILE.DATA<3,POS>
        END ELSE
            BRANCH=1
        END
    END ELSE
        BRANCH=1
    END

    LOCATE "PackageCode" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            PKG.CODE=FILE.DATA<3,POS>
        END
    END ELSE
        PKG.CODE="PACKAGE CODE NOT FOUND"
    END

    LOCATE "Tag" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            TAG=FILE.DATA<3,POS>
        END
    END ELSE
        TAG="TAG NOT FOUND"
    END

    LOCATE "HoldCode" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            HOLD.CODE=FILE.DATA<3,POS>
        END
    END ELSE
        HOLD.CODE="HOLD CODE NOT FOUND"
    END

    LOCATE "AffectDamaged" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            DAMAGED=FILE.DATA<3,POS>
        END
    END ELSE
        DAMAGED="DAMAGED NOT FOUND"
    END

    LOCATE "OriginalQuantity" IN FILE.DATA<1>,START.MARKER SETTING POS THEN     ;* THIS IS THE ORIGINAL QUANTITY IN THE TECSYS SYSTEM. IT SHOULD MATCH MARK QOH
        IF POS > START.MARKER AND POS < END.MARKER THEN
            ORIG.QTY=FILE.DATA<3,POS>
        END
    END ELSE
        ORIG.QTY="ORIGINAL QTY NOT FOUND"
    END

    LOCATE "Quantity" IN FILE.DATA<1>,START.MARKER SETTING POS THEN     ;* QTY FOR THIS TRANSATION
        IF POS > START.MARKER AND POS < END.MARKER THEN
            TRANS.QTY=FILE.DATA<3,POS>
        END
    END ELSE
        TRANS.QTY="TRANSACTION QTY NOT FOUND"
    END

    LOCATE "QuantityProcessed" IN FILE.DATA<1>,START.MARKER SETTING POS THEN        ;* THE UPDATED QTY AFTER PROCESSING THE TRANSACTION
        IF POS > START.MARKER AND POS < END.MARKER THEN
            NEW.QTY=FILE.DATA<3,POS>
        END
    END ELSE
        NEW.QTY="NEW QTY NOT FOUND"
    END

    LOCATE "UserName" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            USER.ID=FILE.DATA<3,POS>
        END ELSE
            USER.ID="Tecsys User"
        END
    END ELSE
        USER.ID="USERNAME NOT FOUND"
    END

    LOCATE "Lot" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            LOT=FILE.DATA<3,POS>
        END
    END ELSE
        LOT="LOT NOT FOUND"
    END

    LOCATE "OutboundOrder" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            OUT.ORD.NUM=FILE.DATA<3,POS>
        END
    END ELSE
        OUT.ORD.NUM="OUTBOUND ORDER NUMBER NOT FOUND"
    END

    LOCATE "OutboundOrderType" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            OUT.ORD.TYPE=FILE.DATA<3,POS>
        END
    END ELSE
        OUT.ORD.TYPE="OUTBOUND ORDER TYPE NOT FOUND"
    END

    LOCATE "OutboundOrderLine" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            OUT.ORD.LINE=FILE.DATA<3,POS>
        END
    END ELSE
        OUT.ORD.LINE="OUTBOUND ORDER LINE NOT FOUND"
    END
        
    LOCATE "OrderStatus" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            ORD.STAT=FILE.DATA<3,POS>
        END
    END ELSE
        ORD.STAT="STATUS NOT FOUND"
    END

    LOCATE "InboundOrder" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            IN.ORD.NUM=FILE.DATA<3,POS>
        END
    END ELSE
        IN.ORD.NUM="INBOUND ORDER NUMBER NOT FOUND"
    END

    LOCATE "InboundOrderType" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            IN.ORD.TYPE=FILE.DATA<3,POS>
        END
    END ELSE
        IN.ORD.TYPE="INBOUND ORDER TYPE NOT FOUND"
    END

    LOCATE "InboundOrderLine" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            IN.ORD.LINE=FILE.DATA<3,POS>
        END
    END ELSE
        IN.ORD.LINE="INBOUND ORDER LINE NOT FOUND"
    END

    LOCATE "OrderLineComplete" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            ORD.LINE.COMP=FILE.DATA<3,POS>
        END
    END ELSE
        ORD.LINE.COMP="ORDER LINE COMPLETE NOT FOUND"
    END

    LOCATE "Container" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            CONTAINER=FILE.DATA<3,POS>
        END
    END ELSE
        CONTAINER="CONTAINER NOT FOUND"
    END

    LOCATE "Location" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            LOCATION=FILE.DATA<3,POS>
        END
    END ELSE
        LOCATION="LOCATION NOT FOUND"
    END

    LOCATE "ReceivingLocation" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            RECV.LOC=FILE.DATA<3,POS>
        END
    END ELSE
        RECV.LOC="RECEIVING LOCATION NOT FOUND"
    END

    LOCATE "SerialNumber" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            SER.NUM=FILE.DATA<3,POS>
        END
    END ELSE
        SER.NUM="SERIAL NUMBER NOT FOUND"
    END

    LOCATE "ItemExpirationDate" IN FILE.DATA<1>,START.MARKER SETTING POS THEN
        IF POS > START.MARKER AND POS < END.MARKER THEN
            ITEM.EXP=FILE.DATA<3,POS>
        END
    END ELSE
        ITEM.EXP="ITEM EXPERATION DATE NOT FOUND"
    END


****************************************************************************
* LOCATING OF ALL STATUS CHANGE DATA IS COMPLETE                           *
* THIS IS WHERE WE CAN DO ADDITIONAL TRANSFORMATION AND VERIFICATION       *
* OF UPDATE DATA IF WE CAN'T OR DON'T WANT TO DO IT DURING THE LOOKUP      *
* WE SHOULD TRY AND KEEP ALL TRANSFORMATION HERE SO LOOKUP LOGIC IS UNIQUE *
****************************************************************************

* BRANCH NUMBER VERIFICATION
IF BRANCH = "BRANCH1" OR BRANCH = "WHSE1" THEN
    BRANCH = "1"
END ELSE
    BRANCH = '1'
END

QTY.CHG = NEW.QTY - ORIG.QTY   ;* THIS SHOULD BE EQUAL TO 'TRANS.QTY' AND WE SHOULD BE DOING A CHECK HERE.

IF TRANS.QTY # QTY.CHG THEN
    QTY.CHG = QTY.CHG * -1
    IF TRANS.QTY # QTY.CHG THEN
        QTY.CHG = 0
    END
END

NEG.QOH = INV.REC<1> + QTY.CHG

IF INV.REC<1> = ORIG.QTY THEN
    IF QTY.CHG = "0" THEN
        QTY.CHG = TRANS.QTY
    END
    
    IF NEG.QOH < "0" THEN
        EMAIL.LIST = 'inv-control@goblueteam.com'
        EMAIL.LIST<-1> = 'support@goblueteam.com'
        CUSTOM.FROM = ''
        CUSTOM.SUBJECT='INVENTORY ADJUSTMENT NEG QOH ATTEMPT PART# ':ITEM.ID
        CUSTOM.BODY = 'MARK QOH: ':INV.REC<1>
        CUSTOM.BODY<-1> = 'TECSYS QOH: ':ORIG.QTY
        CUSTOM.BODY<-1> = 'ATTEMPTED ADJUSTMENT OF: ':QTY.CHG
        CUSTOM.BODY<-1> = 'NO CHANGES HAVE BEEN MADE IN MARK FOR THIS TRANSACTION'
        
        CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)

        RELEASE
        RETURN
    END
    
END ELSE

    IF NEG.QOH < "0" THEN
        EMAIL.LIST = 'inv-control@goblueteam.com'
        EMAIL.LIST<-1> = 'support@goblueteam.com'
        CUSTOM.FROM = ''
        CUSTOM.SUBJECT="TECSYS/MARK INVENTORY DOESN'T MATCH AND NEG QTY ATTEMPTED ON # ":ITEM.ID
        CUSTOM.BODY = 'MARK QOH: ':INV.REC<1>
        CUSTOM.BODY<-1> = 'TECSYS QOH: ':ORIG.QTY
        CUSTOM.BODY<-1> = 'ATTEMPTED ADJUSTMENT OF: ':QTY.CHG
        CUSTOM.BODY<-1> = ' '
        CUSTOM.BODY<-1> = 'NO CHANGES HAVE BEEN MADE IN MARK FOR THIS TRANSACTION'
        
        
        CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)

        RELEASE
        RETURN
    END
    
       EMAIL.LIST = 'inv-control@goblueteam.com'
       EMAIL.LIST<-1> = 'support@goblueteam.com'
       CUSTOM.FROM = ''
        CUSTOM.SUBJECT="TECSYS/MARK INVENTORY DOESN'T MATCH AND NEG QTY ATTEMPTED ON # ":ITEM.ID
       CUSTOM.BODY = 'MARK QOH: ':INV.REC<1>
       CUSTOM.BODY<-1> = 'TECSYS QOH: ':ORIG.QTY
       CUSTOM.BODY<-1> = 'ATTEMPTED ADJUSTMENT TO: ':QTY.CHG
       CUSTOM.BODY<-1> = 'NO CHANGES HAVE BEEN MADE IN MARK FOR THIS TRANSACTION'
       
       CALL SUB.TECSYS.EMAIL.DATA(FILE.DATA,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.MARKER,END.MARKER)
   
       RELEASE
       RETURN
END

TASK = "ADJ"

IF USER.ID = "" OR USER.ID = "wms_erp_si_es" OR USER.ID = "system" THEN
    USER.ID = "WSYS"
END


READ PM.REC FROM F.PRODUCT.MASTER, ITEM.ID ELSE PM.REC = ""    ;*   -  Read in the part information from PM
PM.DESC = PM.REC<1>      ;* - DESC-LONG | DESCRIPTION | LONG-DESC | TRANS.DESC | DESC.PETE --- Description of Part from PM
PM.UNIT = PM.REC<2>     ;*  - UNIT | SELL-UNIT -- UOM in PM
PM.FACTOR = PM.REC<4>      ;*  - FACTR | FACTOR | FCTR  -- Factor of part in PM
IF NOT(NUM(PM.FACTOR)) THEN PM.FACTOR = 0    ;* - Check to make sure data read in is a number - this is good
IF PM.FACTOR + 0 = 0 THEN PM.FACTOR = 1      ;* - this seems to be a null check to make sure later on there is no division by 0 - this is good if we need it for math

CATEG = PM.REC<7>    ;* - CATEG (-CODE|-DESC|-NAME|-VEND) CAT (-MC|-DESC) this is in PM
EXT.COST = OCONV((COST/PM.FACTOR) * QTY.CHG,"md02")   ;* - Extended Cost?  (QTY.CHG=log.variance = quantity - loc.count) AVE-COST inv<7> WHY - BC THE ic FILE IS CODED AS TYPE 1 ref wms.sr.cost

*****************************************************************
* WE HAVE THE UPDATE DATA TRANSFORMED THE WAY WE WANT IT        *
* NOW WE DECIDE WHAT IS IMPORTANT/NEEDED FOR UPDATE AND LOAD IT *
* INTO THE DATA.UPDATE ARRAY PER THE LINE NUMBER                *
*****************************************************************

DATA.UPDATE<1> = TASK
DATA.UPDATE<2> = REC.DATE
DATA.UPDATE<3> = ITEM.ID
DATA.UPDATE<4> = PM.DESC
DATA.UPDATE<5> = PM.UNIT
DATA.UPDATE<8> = "TECSYS ADJUSTMENT"
DATA.UPDATE<9> = QTY.CHG
DATA.UPDATE<10> = EXT.COST
DATA.UPDATE<11> = EXT.COST
DATA.UPDATE<13> = CATEG
DATA.UPDATE<15> = BRANCH
*DATA.UPDATE<17> = code ON THE 5.15 SCREEN
DATA.UPDATE<18> = MONTH.NO
DATA.UPDATE<19> = USER.ID

INV.REC<1> += QTY.CHG

**************************************************************************
* THIS IS WHERE WE CALL ALL UPDATE PROGRAMS FOR ANY WORK TO BE COMPLETED *
* WRITE THE DATA TO THE ORDER AND TO THE TIMESTAMP, ETC ETC              *
**************************************************************************

WRITE INV.REC ON F.INVENTORY,ITEM.ID:"*":BRANCH



READU PH.KEY FROM F.COMPANY, "PRODHIST" ELSE PH.KEY="99999"    ;*   --   Read in the next PH key to be used
LOOP     ;*     --- this is a check to make sure there isn't a duplicate key in PH.
   PH.KEY+=1
   READ JUNK FROM F.PROD.HISTORY, PH.KEY ELSE EXIT
REPEAT
WRITE PH.KEY ON F.COMPANY, "PRODHIST"    ;* -  Write the available record id found back to the counter in the company file
WRITE DATA.UPDATE ON F.PROD.HISTORY, PH.KEY      ;*  -  Write the record update on PH


READU PHP.REC FROM F.PROD.HISTORY.PART, ITEM.ID:"*":MMYY ELSE PHP.REC=""      ;*   - read the part history for the current month
PHP.REC<1,-1> = PH.KEY     ;*  -  update the record with the key
WRITE PHP.REC ON F.PROD.HISTORY.PART, ITEM.ID:"*":MMYY     ;* - Write update on PHP by month

********************************
* GL UPDATE SPECIFIC VARIABLES *
********************************
CAT.REC = ''
INV.ACCT = ''
ADJ.ACCT = ''
CODE.REC = ''
ADJ.COST = ''
GL.REC = ''

*********************
* GET GL ACCOUNTS   *
*********************
READ ACCTS.REC FROM F.COMPANY,'AR-ACCOUNTS' ELSE ACCTS.REC = ''

READ CAT.REC FROM F.PRODUCT.CATEGORY,CATEG ELSE CAT.REC = ''
INV.ACCT = CAT.REC<4>
ADJ.ACCT = CAT.REC<30>

IF ADJ.ACCT = '' THEN ADJ.ACCT = '5420'
IF INV.ACCT = '' THEN INV.ACCT = '1400'

*********************
* CREDIT INV ACCT   *
*********************
ADJ.COST = -EXT.COST
KEY = INV.ACCT:'*':BRANCH:'*':MONTH.NO:'*ADJ*':REC.DATE
READ GL.REC FROM F.AR.GL,KEY ELSE GL.REC = ''
IF ADJ.COST > 0 THEN
    GL.REC<2> = GL.REC<2> + ADJ.COST
END ELSE
    GL.REC<1> = GL.REC<1> - ADJ.COST
END

WRITE GL.REC ON F.AR.GL,KEY

*********************
* DEBIT EXP. ACCT   *
*********************
KEY = ADJ.ACCT:'*':BRANCH:'*':MONTH.NO:'*ADJ*':REC.DATE
READ GL.REC FROM F.AR.GL,KEY ELSE GL.REC = ''

IF ADJ.COST > 0 THEN
    GL.REC<1> = GL.REC<1> + ADJ.COST
END ELSE
    GL.REC<2> = GL.REC<2> - ADJ.COST
END

WRITE GL.REC ON F.AR.GL,KEY

RELEASE
RETURN