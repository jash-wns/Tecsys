SUBROUTINE SUB.TECSYS.EMAIL.DATA(DATA.IN,EMAIL.LIST,CUSTOM.FROM,CUSTOM.SUBJECT,CUSTOM.BODY,START.POS,END.POS)

*PROGRAM TO SEND PARSED XML FILE DATA IN AN EMAIL FOR REVIEW
*WRITTEN BY JASH 06/24/20


* DEFINE CONSTANTS *
AM = CHAR(254)
VM = CHAR(253)
SVM = CHAR(252)


* DEFINE VARIABLES *
EMAIL.FILE=''
KEY.ID=''
NUM.OF.EMAILS=1
START.POINT=START.POS
END.POINT=END.POS
TODAY.DATE = DATE()
TIME.NOW = TIME()
FROM.PROGRAM = SYSTEM(33)

* OPEN FILES *
OPEN '','QTMP' TO TMP.FILE ELSE STOP   ;   * /tmp ON HOST



* CHECK FOR PASSED DATA OR LOAD DEFAULTS
* FOR MULTIPLE DEFAULT EMAILS SEPARATE WITH ATRIBUTE MARKS(AM) OR LOAD IT PER LINE - TO.EMAIL<1>=JASH@WNS1.COM ; TO.EMAIL<2>=SUPPORT@GOBLUETEAM.COM
* EXAMPLE: TO.EMAIL='JShelhamer@goblueteam.com':AM:'support@goblueteam.com':AM:'JASH@WNS1.COM'

IF DATA.IN = '' THEN RETURN   ;   * WE COULD ADD IN SYSTEM ERROR REPORTING HERE IF WE WANTED TO

IF EMAIL.LIST = '' OR EMAIL.LIST = '0' THEN
  TO.EMAIL='WMS-INTErrors@goblueteam.com'
END ELSE
  TO.EMAIL = EMAIL.LIST
END

IF CUSTOM.SUBJECT = '' OR CUSTOM.SUBJECT = '0' THEN
  SUBJECT.EMAIL='Unable to Process Tecsys WMS Data'
END ELSE
  SUBJECT.EMAIL = CUSTOM.SUBJECT
END

IF CUSTOM.FROM = '' OR CUSTOM.FROM = '0' THEN
  FROM.EMAIL='DO_NOT_REPLY@WNS1.COM'
END ELSE
  FROM.EMAIL = CUSTOM.FROM
END

CONVERT VM TO "-" IN FROM.PROGRAM

* PROGRAM START*

NUM.OF.EMAILS=DCOUNT(TO.EMAIL,AM)

******************************************
* BUILD LOOP THROUGH EMAILS AND SEND 
* DOING IT THIS WAY WILL ALLOW FOR EDGE
* CASE MODIFICATIONS WHEN THEY COME UP
******************************************

FOR I = 1 TO NUM.OF.EMAILS

    KEY.ID=TODAY.DATE:"-":TIME.NOW

    EMAIL.FILE<1>='To: ':TO.EMAIL<I>
    EMAIL.FILE<2>='From: ':FROM.EMAIL
    *need to add in optional flag for including calling program or not
*    EMAIL.FILE<3>='Subject: ':SUBJECT.EMAIL:" | ":"CALLING PROGRAM : ":FROM.PROGRAM   ;   * SYSTEM(33) APPENDS THE NAME OF THE PROGRAM THAT CALLED FOR AN EMAIL
    EMAIL.FILE<3>='Subject: ':SUBJECT.EMAIL   
    EMAIL.FILE<4>=''
    
    IF CUSTOM.BODY = '' OR CUSTOM.BODY = "0" THEN
      FOR POS = START.POINT TO END.POINT
        EMAIL.FILE<-1>="Field Name: ":DATA.IN<1,POS>:"  |  ":"Data: ":DATA.IN<3,POS>
        EMAIL.FILE<-1>=''
      NEXT POS
    END ELSE
      EMAIL.FILE<-1> = CUSTOM.BODY
    END
    
    EMAIL.FILE<-1>=''
    EMAIL.FILE<-1>='CALLING PROGRAM : ':FROM.PROGRAM  ;   * SYSTEM(33) APPENDS THE NAME OF THE PROGRAM THAT CALLED FOR AN EMAIL
    EMAIL.FILE<-1>=''
    EMAIL.FILE<-1>='SINCERELY,'
    EMAIL.FILE<-1>=''
    EMAIL.FILE<-1>='YOUR FRIENDLY MARK SYSTEM'

    WRITE EMAIL.FILE ON TMP.FILE,KEY.ID

    *PRINT 'SENDING FILE ': KEY.ID
    EXECUTE 'SENDREC QTMP ':KEY.ID

    * CLEAN UP AND RESET *
    DELETE TMP.FILE,KEY.ID
    EMAIL.FILE=''

NEXT I

RETURN